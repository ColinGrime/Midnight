import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.6'
}

group 'me.colingrimes.midnight'
version '1.2.7'

java {
    sourceCompatibility JavaVersion.VERSION_21
    targetCompatibility JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT'
    implementation 'me.clip:placeholderapi:2.11.6'
    implementation 'com.github.MilkBowl:VaultAPI:1.7.1'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Databases:
    implementation 'com.zaxxer:HikariCP:6.3.0'
    implementation 'org.postgresql:postgresql:42.7.5'
    implementation 'com.mysql:mysql-connector-j:9.2.0'
    implementation 'org.xerial:sqlite-jdbc:3.49.1.0'

    // Testing:
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.42.1'
    testImplementation 'org.xerial:sqlite-jdbc:3.41.2.2'
}

test {
    useJUnitPlatform()
}

processResources {
    filter ReplaceTokens, beginToken: '${', endToken: '}', tokens: ['version': version]
}

shadowJar {
    archiveFileName = "Midnight-${project.version}.jar"
    relocate 'org.bstats', 'me.colingrimes.midnight.libs.bstats'
    dependencies {
        include dependency('org.bstats:bstats-bukkit')
        include dependency('org.bstats:bstats-base')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
